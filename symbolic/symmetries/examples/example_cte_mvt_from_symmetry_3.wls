#!/usr/bin/env wolframscript
(* ::Package:: *)

ClearAll["Global`*"];
(*** Define the model ***)
(* We also assume that omega1 neq omega + 2 ImA1*)
$Assumptions = {Subscript[\[Omega], 1] \[Element] Reals, \[Omega] \[Element] Reals};
A = {{I Subscript[\[Omega], 1]/2, 0, 0, 0, 0, 0, 0},
     {A1, I \[Omega]/2, 0, 0, 0, 0, 0},
     {A1, 0, I \[Omega]/2, 0, 0, 0, 0},
     {A1, 0, 0, I \[Omega]/2, 0, 0, 0},
     {A1, 0, 0, 0, I \[Omega]/2, 0, 0},
     {A61, A61, A62, 0, 0, I Subscript[\[Omega], 6]/2, 0},
     {A71, 0, 0, A74, A75, 0, I Subscript[\[Omega], 7]/2}};
nbNodes = 7;
zvar = Array[z, nbNodes]; 
K[f_] := Sum[Sum[(A[[j, k]]z[[k]] - Conjugate[A[[j, k]]]z[[j]]^2/z[[k]])D[f, z[[j]]], {k, 1, nbNodes}], {j, 1, nbNodes}]

(*** Define the cross-ratio and the symmetry generator ***)

crossRatio[z2_, z3_, z4_, z5_] = ((z4 - z2)(z5 - z3))/((z4 - z3)(z5 - z2));
Print["The cross-ratio is indeed conserved:"]
FullSimplify[K[crossRatio[z[[2]],z[[3]],z[[4]],z[[5]]]]]  

S1[f_] := Sum[Sum[(A[[j, k]]z[[k]] - Conjugate[A[[j, k]]]z[[j]]^2/z[[k]])D[f, z[[j]]], {k, 1, nbNodes}], {j, {2, 3, 6}}] - I Subscript[\[Omega], 1] Sum[z[[j]]D[f, z[[j]]], {j, {2, 3, 6}}]
S2[f_] := Sum[Sum[(A[[j, k]]z[[k]] - Conjugate[A[[j, k]]]z[[j]]^2/z[[k]])D[f, z[[j]]], {k, 1, nbNodes}], {j, {4, 5, 7}}] - I Subscript[\[Omega], 1] Sum[z[[j]]D[f, z[[j]]], {j, {4, 5, 7}}]

commutatorS1[f_] := K[S1[f]] - S1[K[f]]
Print["The symmetry S1 indeed commute (same for S2):"]
FullSimplify[commutatorS1[crossRatio[z[[2]],z[[3]],z[[4]],z[[5]]]]]
FullSimplify[commutatorS1[z[[2]]z[[3]]^2]]

Print["The constants of motion generated by the symmetries are:"]
I1[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]] = FullSimplify[S1[crossRatio[z[[2]],z[[3]],z[[4]],z[[5]]]]]
I2[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]] = FullSimplify[S2[crossRatio[z[[2]],z[[3]],z[[4]],z[[5]]]]]
FullSimplify[I1[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]] + I2[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]]]

Print["The function I1 and I2 are indeed constants of motion:"]
FullSimplify[K[I1[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]]]]
FullSimplify[K[I2[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]]]]

J = D[
   {crossRatio[z[[2]], z[[3]], z[[4]], z[[5]]], 
    I1[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]], 
    I2[z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]]}, 
   {{z[[1]], z[[2]], z[[3]], z[[4]], z[[5]]}}
];

(* Compute the rank of the Jacobian matrix *)
Print["Number of functionally independent constants of motion among c2345, I1, I2:"]
MatrixRank[J]

(* The rank is 2, I1 and I2 differ by a sign only as seen only by inspection *)






